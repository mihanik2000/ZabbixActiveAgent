Rem Отключаем вывод самих команд на экран
@echo off

Rem Включаем расширенную обработку команд
SetLocal
SetLocal EnableExtensions

Rem Предполагаем, что на Windows XP скрипт запускается администратором.
Rem Для более старших систем это неверно.

rem Получаем версию ОС
ver | find "5.1."

rem Windows XP ?
If %errorlevel%==0  (
	rem Пропускаем проверку админских прав
	GOTO SKIPADMIN
 )
 
SET HasAdminRights=0

FOR /F %%i IN ('WHOAMI /PRIV /NH') DO (
	IF "%%i"=="SeTakeOwnershipPrivilege" SET HasAdminRights=1
)

IF NOT %HasAdminRights%==1 (
	ECHO .
	ECHO Этот скрипт необходимо запускать от имени администратора.
	ECHO .
	GOTO END
)

rem включим режим электропитания "Высокая производительность"
powercfg /setactive 8c5e7fda-e8bf-4a96-9a85-a6e23a8c635c	

:SKIPADMIN

rem Разрешим отвечать на ping 
netsh firewall set icmpsetting 8

rem разрешаем входящие запросы от сервера
netsh advfirewall firewall add rule name="zabbix_in" protocol="TCP" localport=10050 action=allow dir=IN
netsh firewall set portopening tcp 10050 zabbix_in enable

If Exist "C:\Program Files (x86)" (
	copy /y C:\zabbix\bin\win64\*.exe C:\zabbix\
) else ( 
	copy /y C:\zabbix\bin\win32\*.exe C:\zabbix\
)

rem Устанавливаем заббикс
c:/zabbix/zabbix_agentd.exe --config c:/zabbix/zabbix_agentd.win.conf --install
net start "Zabbix Agent"

:CONTINUE
	ECHO .
	ECHO Всё!
	
:END

EXIT /B
