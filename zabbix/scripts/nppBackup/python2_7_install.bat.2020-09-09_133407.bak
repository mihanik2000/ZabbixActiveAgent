@Rem Отключаем вывод самих команд на экран
@echo off

Rem Предполагаем, что на Windows XP скрипт запускается администратором.
Rem Для более старших систем это неверно.

rem Получаем версию ОС
ver | find "5.1."

rem Windows XP ?
If %errorlevel%==0  (
	rem Пропускаем проверку админских прав
	GOTO SKIPADMIN
 )
 
SET HasAdminRights=0

FOR /F %%i IN ('WHOAMI /PRIV /NH') DO (
	IF "%%i"=="SeTakeOwnershipPrivilege" SET HasAdminRights=1
)

IF NOT %HasAdminRights%==1 (
	ECHO .
	ECHO Этот скрипт необходимо запускать от имени администратора.
	ECHO .
	GOTO END
)

:SKIPADMIN

rem устанавливаем только 32-разрядный Python, т.к. скриптам много памяти не нужно.
start /wait msiexec.exe /passive /i "%~dp0python-2.7.18.msi" /norestart ALLUSERS=1 ADDLOCAL=ALL


rem Ассоциируем файлы .py с интерпретатором Python и настраиваем PATH
setx PATH "C:\Python27\;C:\Python27\Scripts;%Path%"
assoc .py=Python.File
ftype Python.File=C:\Python27\python.exe "%1" %*

rem Устанавливаем дополнительные модули
C:\Python27\Scripts\pip.exe install pywin32
C:\Python27\Scripts\pip.exe install WMI

ECHO .
ECHO Всё!
ECHO .
	
:END

PAUSE

EXIT /B